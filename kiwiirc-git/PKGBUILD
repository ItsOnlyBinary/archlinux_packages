# Maintainer: None
pkgname=kiwiirc-git
pkgver=r1480.feba0f0
pkgrel=1
pkgdesc="Kiwi IRC Frontend"
arch=('i686' 'x86_64')
url="https://www.kiwiirc.com"
license=('Apache License 2.0')
backup=('etc/kiwiirc/client.json')
makedepends=('git' 'nodejs' 'yarn')
conflicts=(kiwiirc)
source=('kiwiirc::git+https://github.com/kiwiirc/kiwiirc.git#branch=master')
sha1sums=('SKIP')

pkgver() {
  cd "$srcdir/kiwiirc"
  printf "r%s.%s" "$(git rev-list --count HEAD)" "$(git rev-parse --short HEAD)"
}

prepare() {
  cd "$srcdir/kiwiirc"
  yarn install --no-progress
}

build() {
  cd "$srcdir/kiwiirc"
  yarn run build
}

package() {
  cd "$srcdir"
  
  # Copy license file
  install -Dm644 "$srcdir/kiwiirc/LICENSE" "$pkgdir/usr/share/licenses/$pkgname/LICENSE"

  # Copy client files
  install -dm755 "$pkgdir/usr/share/kiwiirc/"
  cp -rd "$srcdir/kiwiirc/dist/"* "$pkgdir/usr/share/kiwiirc/"
  rm -f "$pkgdir/usr/share/kiwiirc/module_filesize_report.html"

  # Move client json to etc and symlink
  install -dm755 "$pkgdir/etc/kiwiirc/"
  mv "$pkgdir/usr/share/kiwiirc/static/config.json" "$pkgdir/etc/kiwiirc/client.json"
  ln -s "$pkgdir/etc/kiwiirc/client.json" "$pkgdir/usr/share/kiwiirc/static/config.json"
}

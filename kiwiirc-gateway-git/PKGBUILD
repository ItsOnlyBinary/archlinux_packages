# Maintainer: None
pkgname=kiwiirc-gateway-git
pkgver=r157.ef93dab
pkgrel=1
pkgdesc="Kiwi IRC server"
arch=('i686' 'x86_64')
url="https://www.kiwiirc.com"
license=('Apache License 2.0')
backup=('etc/kiwiirc/config.conf')
makedepends=('git' 'go' 'nodejs' 'yarn')
install=kiwiirc.install
conflicts=(kiwiirc kiwiirc-git)

source=(kiwiirc.service kiwiirc.install kiwiirc.conf.sysusers config.patch)
sha1sums=('03cbabcc7a480bdfe921e577f80b186cddf19740'
          '9b0df774f27558b93e4bd2ecbc359a92200bbf6e'
          'a2f5db846baa00298ace29e942b3d4bff08ce849'
          '5afb9a098053d6f271ef747e98b7c48b31de6524')

pkgver() {
  cd $srcdir/go/src/github.com/kiwiirc/webircgateway
  printf "r%s.%s" "$(git rev-list --count HEAD)" "$(git rev-parse --short HEAD)"
}

prepare() {
  cd $srcdir
  mkdir -p go
  export GOPATH=$srcdir/go

  # Prepare server files
  go get github.com/kiwiirc/webircgateway
}

build() {
  cd $srcdir/go/src/github.com/kiwiirc/webircgateway
  export GOPATH=$srcdir/go
  go build -o kiwiirc main.go
}

package() {
  cd $srcdir
  
  # Copy systemd files
  install -Dm644 kiwiirc.service $pkgdir/usr/lib/systemd/system/kiwiirc.service
  install -Dm644 kiwiirc.conf.sysusers $pkgdir/usr/lib/sysusers.d/kiwiirc.conf

  # Copy binary, config & license
  install -Dm755 $srcdir/go/src/github.com/kiwiirc/webircgateway/kiwiirc $pkgdir/usr/bin/kiwiirc
  install -Dm600 $srcdir/go/src/github.com/kiwiirc/webircgateway/config.conf.example $pkgdir/etc/kiwiirc/config.conf
  install -Dm644 $srcdir/go/src/github.com/kiwiirc/webircgateway/LICENSE $pkgdir/usr/share/licenses/kiwiirc-gateway-git/LICENSE

  # Set correct webroot in config
  cd $pkgdir/etc/kiwiirc/
  git apply $srcdir/config.patch
}
